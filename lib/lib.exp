#!/usr/bin/expect

proc send_screen_quit_cmd {} {
    # Send Ctrl-A to screen
    send "\x01"
    send ":quit\r"
}

proc wait_for_text { wait_txt } {
	expect {
		$wait_txt {
		}
		timeout {
			send_error "Failed while waiting for '$wait_txt'\n"
			exit 1
		}
	}
}

proc wait_for_prompt { wait_cmd } {
	set multiPrompt {[#$] }

	expect {
		-re $multiPrompt {
		}
		timeout {
			send_error "Failed to get Linux prompt after '$wait_cmd'\n"
			exit 1
		}
	}
}

proc send_wait_for_prompt { cmd } {
	send $cmd
	wait_for_prompt $cmd
}

proc wait_for_value_in_range { min max units } {
	expect {
		-re "\[0-9]{3,4}" {
			set in $expect_out(0,string)

			if { $in < $min } {
				send_error "Value ($in) below limit $min $units\n"
				exit 1
			}

			if { $in > $max } {
				send_error "Value ($in) above limit $max $units\n"
				exit 1
			}
		}
		timeout {
			send_error "Failed to read value in range '$min - $max $units'\n"
			exit 1
		}
	}
}
